buildscript {
    ext {
        springBootVersion = '1.5.3.RELEASE'
        okhttp_version = '3.4.2'
        junit_version = '4.12'
        assertj_version = '3.7.0'
        pactjvm_version = '3.3.9'
        wiremock_version = '2.19.0'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'eclipse'

version = '1.0.0-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    cdcTests
}

eclipse {
    classpath {
        plusConfigurations.add configurations.cdcTestsCompile
    }
}

task cdcTest(type: Test) {
    systemProperties['pact.rootDir'] = "../pacts"
    testClassesDir = sourceSets.cdcTests.output.classesDir
    classpath = sourceSets.cdcTests.runtimeClasspath
}

check.dependsOn cdcTest

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile "com.squareup.okhttp3:okhttp:${okhttp_version}"
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile "junit:junit:${junit_version}"
    testCompile "com.github.tomakehurst:wiremock:${wiremock_version}"

    cdcTestsCompile sourceSets.main.output
    cdcTestsCompile sourceSets.test.output
    cdcTestsCompile "org.springframework.boot:spring-boot-starter-web"
    cdcTestsCompile "com.squareup.okhttp3:okhttp:${okhttp_version}"
    cdcTestsCompile "org.springframework.boot:spring-boot-starter-test"
    cdcTestsCompile "org.assertj:assertj-core:${assertj_version}"
    cdcTestsCompile "au.com.dius:pact-jvm-consumer-junit_2.11:${pactjvm_version}"
}
